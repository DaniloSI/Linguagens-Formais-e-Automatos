import dominio.Politico;
import dominio.Esperanca;

rule "Nos temos um politico honesto"
    salience 10

    when
        exists( Politico( honesto == true ) )
    then
        insertLogical( new Esperanca() );
end

rule "A esperanca vive"
    salience 10
    when
        exists( Esperanca() )
    then
        System.out.println("Hurrah!!! A democracia vive!");
end

rule "A esperanca esta morta"
    when
        not( Esperanca() )
    then
        System.out.println( "Estamos todos condenados!!! A democracia esta morta." );
end

rule "Corrompe o(s) honesto(s)"
    when
        $politico : Politico( honesto == true )
        exists( Esperanca() )
    then
        System.out.println( "Eu sou uma corporacao do mal e tenho corrompido o(a) " + $politico.getNome() );
        modify( $politico ) {
            setHonesto( false )
        }
end